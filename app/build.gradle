buildscript {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_VELOCIRAPTOR
                keyPassword RELEASE_KEY_PASSWORD_VELOCIRAPTOR
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
    }

    defaultConfig {
        applicationId "com.pluscubed.velociraptor"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 59
        versionName "1.7.2"
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [appName: "@string/app_name"]

        lintOptions {
            disable 'MissingTranslation'
            disable 'ExtraTranslation'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }

        debugIap {
            debuggable true
            multiDexEnabled true

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }

        debug {
            multiDexEnabled true

            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            testCoverageEnabled false
            manifestPlaceholders = [appName: "Velociraptor DEBUG"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    productFlavors {
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://kotlin.bintray.com/kotlinx/" }
}

ext{
    retrofit_version = '2.9.0'
    okhttp_version = '3.14.9'
    jackson_version = '2.12.0'
    room_version = '2.2.5'
    butterknife_version = '10.2.3'
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.robolectric:robolectric:4.4'

    //debugImplementation 'im.dino:dbinspector:3.4.1@aar'
    //debugIapImplementation 'im.dino:dbinspector:3.4.1@aar'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.2.1"

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation "androidx.lifecycle:lifecycle-service:2.2.0"

    implementation "com.google.firebase:firebase-core:18.0.0"
    implementation "com.google.firebase:firebase-messaging:21.0.0"
    implementation "com.google.firebase:firebase-config:20.0.2"

    implementation "com.android.billingclient:billing:1.2.2"

    implementation "com.google.android.gms:play-services-location:17.1.0"
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    implementation "com.google.android.gms:play-services-maps:17.0.0"
    implementation "com.google.maps.android:android-maps-utils:2.2.0"

    implementation 'com.afollestad.material-dialogs:core:3.3.0'

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-jackson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation 'com.squareup.okio:okio:2.9.0'

    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"

    implementation 'com.github.gigamole.arcprogressstackview:library:1.0.3'

    implementation 'com.github.pluscubed:recycler-fast-scroll:0.3.2'

    implementation 'com.github.bumptech.glide:glide:3.8.0'

    implementation "com.jakewharton:butterknife:$butterknife_version"
    kapt "com.jakewharton:butterknife-compiler:$butterknife_version"

    implementation 'com.jakewharton.timber:timber:4.7.1'

    // LeakCanary temporarily disabled
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'

    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'
    implementation 'com.google.firebase:firebase-analytics:18.0.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0-alpha05'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
}


apply plugin: 'com.google.gms.google-services'